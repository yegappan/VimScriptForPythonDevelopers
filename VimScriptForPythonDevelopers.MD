# Vim Script for Python Developers

This is a guide to Vim Script development for Python developers. Sample code for the various expressions, statements, functions and programming constructs is shown in both Python and Vim Script. This is not intended to be a tutorial for developing Vim scripts. It is assumed that the reader is familiar with Python programming.

For an introduction to Vim Script development, refer to [usr_41.txt](https://vimhelp.org/usr_41.txt.html), [eval.txt](https://vimhelp.org/eval.txt.html) and [Learn Vimscript the Hard Way](https://learnvimscriptthehardway.stevelosh.com/)

For a guide similar to this one for JavaScript developers, refer to [Vim Script for the JavaScripter](https://w0rp.com/blog/post/vim-script-for-the-javascripter/)

This guide only describes the programming constructs that are present in both Python and Vim. The constructs that are unique to Vim (e.g. [autocommands](https://vimhelp.org/autocmd.txt.html#autocommands), [key-mapping](https://vimhelp.org/map.txt.html#key-mapping), [abbreviations](https://vimhelp.org/map.txt.html#abbreviations), [user-commands](https://vimhelp.org/map.txt.html#user-commands), [plugins](https://vimhelp.org/usr_05.txt.html#plugin), etc.) are not described in this guide.

Some of the features described in this guide are available only in the recent versions of Vim (8.2).

The Github repository for this gist is available at: https://github.com/yegappan/VimScriptForPythonDevelopers.

------------------------------------------------------------------------------

## Table of Contents
* [Literals](#literals)
* [Variables](#variables)
* [Operators](#operators)
* [String](#string)
* [List](#list)
* [Dictionary](#dictionary)
* [If Statement](#if-statement)
* [For Loop](#for-loop)
* [While Loop](#while-loop)
* [Comment](#comment)
* [Function](#function)
* [Lambda Function](#lambda-function)
* [Partial Function](#partial-function)
* [Closure](#closure)
* [Class](#class)
* [Exception Handling](#exception-handling)
* [Line Continuation](#line-continuation)
* [File Operations](#file-operations)
* [Directory Operations](#directory-operations)
* [Random Numbers](#random-numbers)
* [Mathematical Functions](#mathematical-functions)
* [Date/Time Functions](#datetime-functions)
* [External Commands](#external-commands)
* [User Input/Output](#user-inputoutput)
* [Environment Variables](#environment-variables)
* [Command Line Arguments](#command-line-arguments)
* [Regular Expressions](#regular-expressions)
* [Binary Data](#binary-data)
* [Timers](#timers)
* [JSON encoder and decoder](#json-encoder-and-decoder)
* [Network Sockets](#network-sockets)
* [Background Processes](#background-processes)
* [Unit Tests](#unit-tests)

------------------------------------------------------------------------------

## Literals

Type| Python | VimScript
----|--------|---------
integer| `-1, 0, 5` | `-1, 0, 5`
binary| `0b1011` | `0b1011`
octal| `0o477` | `0477`
hexadecimal| `0x1AE` | `0x1AE`
float| `3.14, -1.5e2` | `3.14, -1.5e2`
string| `"hello", 'world'` | `"hello", 'world'`
boolean| `True, False` | `v:true, v:false`
list| `[], [5, 9], ['a', 'b']` | `[], [5, 9], ['a', 'b']`
dict| `{}, {'idx' : 2, 'name' : 'abc'}` | `{}, {'idx' : 2, 'name' : 'abc'}`
special| `None` | `v:none, v:null`

*Help:* [expr-number](https://vimhelp.org/eval.txt.html#expr-number), [string](https://vimhelp.org/eval.txt.html#string), [Boolean](https://vimhelp.org/eval.txt.html#Boolean), [list](https://vimhelp.org/eval.txt.html#list), [dict](https://vimhelp.org/eval.txt.html#dict), [v:none](https://vimhelp.org/eval.txt.html#v:none), [v:null](https://vimhelp.org/eval.txt.html#v:null)

------------------------------------------------------------------------------

## Variables

### Creating a Variable
**Python:**
```python
i = 10
pi = 3.1415
str = "Hello"
a, b, s = 10, 20, "sky"
```

**VimScript:**
```vim
let i = 10
let pi = 3.1415
let str = "Hello"
let [a, b, s] = [10, 20, "sky"]
```

*Help:* [variables](https://vimhelp.org/eval.txt.html#variables)

### Deleting a Variable
**Python:**
```python
del str
```

**VimScript:**
```vim
unlet str
```

*Help:* [:unlet](https://vimhelp.org/eval.txt.html#%3Aunlet)

### Here Document
Assigning multi-line values to a variable.

**Python:**
```python
import textwrap
i = textwrap.dedent("""
    one
    two three
      four
    five
""".lstrip('\n')).splitlines()
# i == ['one', 'two three', '  four', 'five']
```

**VimScript:**
```vim
let i =<< trim END
    one
    two three
      four
    five
END
" i == ['one', 'two three', '  four', 'five']
```

*Help:* [:let-heredoc](https://vimhelp.org/eval.txt.html#%3Alet-heredoc)

### Types

Type|Python|VimScript
----|------|---------
Number| `num = 10` | `let num = 10`
Float| `f = 3.4` | `let f = 3.4`
Booelan| `done = True` | `let done = v:true`
String| `str = "green"` | `let str = "green"`
List| `l = [1, 2, 3]` | `let l = [1, 2, 3]`
Dictionary| `d = {'a' : 5, 'b' : 6}` | `let d = #{a : 5, b : 6}`

*Help:* [variables](https://vimhelp.org/eval.txt.html#variables)

### Type Conversion

Conversion|Python|VimScript
----------|------|---------
Number to Float| `float(n)` | `floor(n)`
Float to Number| `int(f)` | `float2nr(f)`
Number to String| `str(n)` | `string(n)`
String to Number| `int(str)` | `str2nr(str)`
Float to String| `str(f)` | `string(f)`
String to Float| `float(str)` | `str2float(str)`
List to String| `str(l)` | `string(l)`
String to List| `eval(str)` | `eval(str)`
Dict to String| `str(d)` | `string(d)`
String to Dict| `eval(str)` | `eval(str)`

*Help:* [string()](https://vimhelp.org/builtin.txt.html#string%28%29), [str2nr()](https://vimhelp.org/builtin.txt.html#str2nr%28%29), [str2float()](https://vimhelp.org/builtin.txt.html#str2float%28%29), [float2nr()](https://vimhelp.org/builtin.txt.html#float2nr%28%29), [floor()](https://vimhelp.org/builtin.txt.html#floor%28%29), [eval()](https://vimhelp.org/builtin.txt.html#eval%28%29)

### Type Check

Type|Python|VimScript
----|------|---------
Number| `isintance(x, int)` | `type(x) is v:t_number`
String| `isinstance(x, str)` | `type(x) is v:t_string`
List| `isintance(x, list)` | `type(x) is v:t_list`
Dictionary| `isintance(x, dict)` | `type(x) is v:t_dict`
Float| `isintance(x, float)` | `type(x) is v:t_float`
Boolean| `isinstance(x, bool)` | `type(x) is v:t_bool`

*Help:* [type()](https://vimhelp.org/builtin.txt.html#type%28%29)

### Variable Namespaces

All the variables in VimScript have a scope. The scope is specified by prefixing the variable name with one of the prefix strings shown in the table below. If a variable name is used without a scope prefix, then inside a function the function local scope is used. Otherwise the global variable scope is used.

Scope Prefix|Description
-----|-----------
g: | global
l: | function-local
s: | script-local
a: | function argument
v: | internal
b: | buffer local
w: | window local
t: | tab local

*Help:* [global-variable](https://vimhelp.org/eval.txt.html#global-variable), [local-variable](https://vimhelp.org/eval.txt.html#local-variable), [script-variable](https://vimhelp.org/eval.txt.html#script-variable), [function-argument](https://vimhelp.org/eval.txt.html#function-argument), [vim-variable](https://vimhelp.org/eval.txt.html#vim-variable), [buffer-variable](https://vimhelp.org/eval.txt.html#buffer-variable), [window-variable](https://vimhelp.org/eval.txt.html#window-variable), [tabpage-variable](https://vimhelp.org/eval.txt.html#tabpage-variable)

------------------------------------------------------------------------------

## Operators

### Arithmetic Operators

What|Python|VimScript
----|------|---------
addition| `a = 10 + 20` | `let a = 10 + 20`
subtraction| `a = 30 - 10` | `let a = 30 - 10`
multiplication| `a = 3 * 5` | `let a = 3 * 5`
division| `a = 22 / 7` | `let a = 22 / 7`
modulus| `a = 10 % 3` | `let a = 10 % 3`
exponentiation| `a = 2 ** 3` | `let a = float2nr(pow(2, 3))`
floor division| `a = 10 // 3` | `let a = floor(10 / 3.0)`

*Help:* [expr5](https://vimhelp.org/eval.txt.html#expr5)

### Assignment Operators

What|Python|VimScript
----|------|---------
addition| `a += 1` | `let a += 1`
subtraction| `a -= 2` | `let a -= 2`
multiplication| `a *= 4` | `let a *= 4`
division| `a /= 2` | `let a /= 2`
modulus| `a %= 2` | `let a %= 2`

*Help:* [:let+=](https://vimhelp.org/eval.txt.html#%3alet+=)

### Comparison Operators

What|Python|VimScript
----|------|---------
equal to| `a == b` | `a == b`
not equal to| `a != b` | `a != b`
greater than| `a > b` | `a > b`
less than| `a < b` | `a < b`
greater than or equal to| `a >= b` | `a >= b`
less than or equal to| `a <= b` | `a <= b`

*Help:* [expr4](https://vimhelp.org/eval.txt.html#expr4)

### Logical Operators

What|Python|VimScript
----|------|---------
logical and| `x and y` | `x && y`
logical or| `x or y` | `x \|\| y`
logical not| `not x` | `!x`

*Help:* [expr2](https://vimhelp.org/eval.txt.html#expr2)

### Bitwise Operators

What|Python|VimScript
----|------|---------
bitwise AND| `c = a & b` | `let c = and(a, b)`
bitwise OR| `c = a \| b` | `let c = or(a, b)`
bitwise NOT| `c = ~a` | `let c = invert(a)`
bitwise XOR| `c = a ^ b` | `let c = xor(a, b)`
left shift| `c = a << b` | `let c = a * float2nr(pow(2, b))`
right shift| `c = a >> b` | `let c = float2nr(floor(a / pow(2, b)))`

*Help:* [and()](https://vimhelp.org/builtin.txt.html#and%28%29), [or()](https://vimhelp.org/builtin.txt.html#or%28%29), [invert()](https://vimhelp.org/builtin.txt.html#invert%28%29), [xor()](https://vimhelp.org/builtin.txt.html#xor%28%29)

### Identity Operators

What|Python|VimScript
----|------|---------
is| `x is y` | `x is y`
is not| `x is not y` | `x isnot y`

*Help:* [expr-is](https://vimhelp.org/eval.txt.html#expr-is), [expr-isnot](https://vimhelp.org/eval.txt.html#expr-isnot)

------------------------------------------------------------------------------

## String

Vim has functions for dealing with both multibyte and non-multibyte strings. Some functions deal with bytes and some functions deal with characters. Refer to the reference guide for the various functions to get the detailed information.

### Raw string

**Python:**
```python
s1 = r"one\ntwo\n"
s2 = "one\ntwo\n"
```

**VimScript:**
```vim
let s1 = 'one\ntwo\n'
let s2 = "one\ntwo\n"
```

*Help:* [literal-string](https://vimhelp.org/eval.txt.html#literal-string), [string](https://vimhelp.org/eval.txt.html#string)

### Finding string length

**Python:**
```python
n = len("Hello World")
```

**VimScript:**
```vim
" count the number of bytes in a string
let n = len("Hello World")
" count the number of bytes in a string
let n = strlen("Hello World")
" count the number of characters in a string
let n = strwidth("Hello World")
```

*Help:* [len()](https://vimhelp.org/builtin.txt.html#len%28%29), [strlen()](https://vimhelp.org/builtin.txt.html#strlen%28%29), [strwidth()](https://vimhelp.org/builtin.txt.html#strwidth%28%29), [strdisplaywidth()](https://vimhelp.org/builtin.txt.html#strdisplaywidth%28%29)

### String Concatenation

**Python:**
```python
str1 = "blue"
str2 = "sky"
s = str1 + str2
```

**VimScript:**
```vim
let str1 = "blue"
let str2 = "sky"
let s = str1 .. str2

" Deprecated way of concatenating strings
let s = str1 . str2
```

### String Comparison

**Python:**
```python
# compare strings matching case
str1 = "blue"
str2 = "blue"
str3 = "sky"
if str1 == str2:
  print("str1 and str2 are same")
if str1 is str2:
  print("str1 and str2 are same")
if str1 != str3:
  print("str1 and str3 are not same")
if str1 is not str3:
  print("str1 and str3 are not same")

# compare strings ignoring case
str1 = "Blue"
str2 = "BLUE"
str3 = "sky"
if str1.lower() == str2.lower():
  print("str1 and str2 are same")
if str1.lower() != str3.lower():
  print("str1 and str3 are not same")
```

**VimScript:**
```vim
" compare strings matching case
let str1 = "blue"
let str2 = "blue"
let str3 = "sky"
if str1 ==# str2
  echo "str1 and str2 are same"
endif
if str1 is# str2
  echo "str1 and str2 are same"
endif
if str1 !=# str3
  echo "str1 and str3 are not same"
endif
if str1 isnot# str3
  echo "str1 and str3 are not same"
endif

" compare strings ignoring case
let str1 = "Blue"
let str2 = "BLUE"
let str3 = "sky"
if str1 ==? str2
  echo "str1 and str2 are same"
endif
if str1 is? str2
  echo "str1 and str2 are same"
endif
if str1 !=? str3
  echo "str1 and str3 are not same"
endif
if str1 isnot? str3
  echo "str1 and str3 are not same"
endif
```

*Help:* [expr4](https://vimhelp.org/eval.txt.html#expr4)

### Getting a substring

In Python, when using an index range to specify a sequence of characters in a string, the character at the end index is not included. In Vim, the character at the end index is included.

**Python:**
```python
str = "HelloWorld"
sub = str[2:5]
```

**VimScript:**
```vim
let str = "HelloWorld"
" use byte index range
let sub = str[2:4]
" use a negative byte range
let sub = str[-3:]
let sub = str[2:-3]
" use byte index and length
let sub = strpart(str, 2, 3)
" use character index and length
let sub = strcharpart(str, 2, 3)
```

*Help:* [expr-[:]](https://vimhelp.org/eval.txt.html#expr-[%3a]), [strpart()](https://vimhelp.org/builtin.txt.html#strpart%28%29)

### Counting the occurrences of a substring

**Python:**
```python
str = "Hello World"
c = str.count("l")
```

**VimScript:**
```vim
let str = "Hello World"
let c = str->count("l")
```

*Help:* [count()](https://vimhelp.org/builtin.txt.html#count%28%29)

### Finding the position of a substring

**Python:**
```python
str = "running"
idx = str.find("nn")    # leftmost
idx = str.rfind("ing")  # rightmost
# idx == -1 if the substring is not present
```

**VimScript:**
```vim
let str = "running"
let idx = str->stridx("nn")    " leftmost
let idx = str->strridx("ing")  " rightmost
" idx == -1 if the substring is not present
```

*Help:* [stridx()](https://vimhelp.org/builtin.txt.html#stridx%28%29), [strridx()](https://vimhelp.org/builtin.txt.html#strridx%28%29)

### Checking whether a string starts with or ends with a substring

**Python:**
```python
str = "running"
if str.startswith("run"):
    print("starts with run")
if str.endswith("ing"):
    print("ends with ing")
```

**VimScript:**
```vim
let str = "running"
if str =~# '^run'
  echo "starts with run"
endif
if str[:len('run') - 1] is# 'run'
  echo "starts with run"
endif

if str =~# 'ing$'
  echo "ends with ing"
endif
```

*Help:* [expr-=~#](https://vimhelp.org/eval.txt.html#expr-=~#)

### Joining strings in a List with a separator

**Python:**
```python
s = ":".join(['ab', 'cd', 'ef'])
```

**VimScript:**
```vim
let s = join(['ab', 'cd', 'ef'], ':')
```

*Help:* [join()](https://vimhelp.org/builtin.txt.html#join%28%29)

### Changing the case of letters in a string

**Python:**
```python
s = "Hello World"
l = s.lower()
l = s.upper()
```

**VimScript:**
```vim
let s = "Hello World"
let l = s->tolower()
let l = s->toupper()
```

*Help:* [tolower()](https://vimhelp.org/builtin.txt.html#tolower%28%29), [toupper()](https://vimhelp.org/builtin.txt.html#toupper%28%29)

### Replace a substring

**Python:**
```python
s = "Hello World"
s2 = s.replace("Hello", "New")
```

**VimScript:**
```vim
let s = "Hello World"
let s2 = s->substitute("Hello", "New", 'g')
```

*Help:* [substitute()](https://vimhelp.org/builtin.txt.html#substitute%28%29)

### Split a string

**Python:**
```python
s = "a:b:c"
s2 = s.split(":")
```

**VimScript:**
```vim
let s = "a:b:c"
let s2 = s->split(":")
```

*Help:* [split()](https://vimhelp.org/builtin.txt.html#split%28%29), [join()](https://vimhelp.org/builtin.txt.html#join%28%29),

### Stripping leading and trailing whitespace from a string

**Python:**
```python
s = "  python  "
# strip leading and trailing whitespace
s2 = s.strip()
print(f"<{s2}>")
# strip leading space characters
s2 = s.lstrip(' ')
print(f"<{s2}>")
# strip trailing space characters
s2 = s.rstrip(' ')
print(f"<{s2}>")
```

**VimScript:**
```vim
let s = "  vim  "
" strip leading and trailing whitespace
let s2 = s->trim()
echo $"<{s2}>"
" strip leading space characters
let s2 = s->trim(' ', 1)
echo $"<{s2}>"
" strip trailing space characters
let s2 = s->trim(' ', 2)
echo $"<{s2}>"
```

*Help:* [trim()](https://vimhelp.org/builtin.txt.html#trim%28%29)

### Checking whether a string contains specific type of characters

**Python:**
```python
s = "text"
if s.isalnum():
    print("Contains only alphanumeric characters")
if s.isalpha():
    print("Contains only alphabetic characters")
if s.isdigit():
    print("Contains only digits")
if s.isspace():
    print("Contains only whitespace characters")
if s.isupper():
    print("Contains only uppercase characters")
if s.islower():
    print("Contains only lowercase characters")
```

**VimScript:**
```vim
let s = "text"
if s =~ '^[:alnum:]\+'
  echo "Contains only alphanumeric characters"
endif
if s =~ '^\a\+$'
  echo "Contains only alphabetic characters"
endif
if s =~ '^\d\+$'
  echo "Contains only digits"
endif
if s =~ '^\s\+$'
  echo "Contains only whitespace characters"
endif
if s =~ '^\u\+$'
  echo "Contains only uppercase characters"
endif
if s =~ '^\l\+$'
  echo "Contains only lowercase characters"
endif
```

*Help:* [/collection](https://vimhelp.org/pattern.txt.html#%2fcollection)

### Data type conversion to string

**Python:**
```python
s = str(268)
s = str(22.7)
s = str([1, 2, 3])
s = str({'a' : 1, 'b' : 2})
```

**VimScript:**
```vim
let s = string(268)
let s = string(22.7)
let s = string([1, 2, 3])
let s = string({'a' : 1, 'b' : 2})
```

*Help:* [string()](https://vimhelp.org/builtin.txt.html#string%28%29)

### Evaluating a string

**Python:**
```python
x = 10
y = eval("x * 2")
print(y)
n = eval("min([4, 3, 5])")
```

**VimScript:**
```vim
let x = 10
let y = eval("x * 2")
echo y
let n = eval("min([4, 3, 5])")
```

*Help:* [eval()](https://vimhelp.org/builtin.txt.html#eval%28%29)

### Executing commands in a string

**Python:**
```python
exec("for i in range(5):\n    print(i)\n")
```

**VimScript:**
```vim
execute "for i in range(5)\necho i\nendfor"
```

*Help:* [:execute](https://vimhelp.org/eval.txt.html#:execute)

### Substituting variables in a string (String Interpolation)

**Python:**
```python
aVar = 10
str = f"Value of 'a' is {aVar}"
print(str)

bList = [1, 2, 3]
str = f"bList is {bList}"
print(str)
```

**VimScript:**
```vim
let aVar = 10
let str = $"Value of 'a' is {aVar}"
echo str

let bList = [1, 2, 3]
let str = $"bList is {string(bList)}"
echo str
```

*Help:* [interp-string](https://vimhelp.org/eval.txt.html#interp-string)

### Getting the Unicode value of a character and vice versa

**Python:**
```python
print("Ordinal value of 'a' is " + str(ord("a")))
print("Character with value 65 is " + chr(65))
```

**VimScript:**
```vim
echo "Ordinal value of 'a' is " .. char2nr('a')
echo "Character with value 65 is " .. nr2char(65)
```

*Help:* [char2nr()](https://vimhelp.org/builtin.txt.html#chr%28%29), [nr2char()](https://vimhelp.org/builtin.txt.html#or%28%29)

### Getting the character values of a string

**Python:**
```python
l = [ord(i) for i in 'Hello']
s = ''.join(chr(i) for i in l)
print(s)
print(l)
```

**VimScript:**
```vim
let l = str2list('Hello')
let s = list2str(l)
echo s
echo l
```

*Help:* [str2list()](https://vimhelp.org/builtin.txt.html#str2list%28%29), [list2str()](https://vimhelp.org/builtin.txt.html#list2str%28%29)

### String Methods

Method|Python|VimScript
----|------|---------
capitalize()| `'one two'.capitalize()` | `'one two'->substitute('.', '\u&', '')`
center()| `'abc'.center(10)` | `Not available`
count()| `"abbc".count('b')` | `"abbc"->count('b')`
decode()| `str.decode()` | `Not available`
encode()| `str.encode()` | `Not available`
endswith()| `'running'.endswith('ing')` | `'running' =~# 'ing$'`
expandtabs()| `"a\tb".expandtabs()` | `"a\tb"->substitute("\t", repeat(' ', 8), 'g')`
find()| `"running".find('nn')` | `"running"->stridx('nn')`
index()| `'hello'.index('e')` | `'hello'->stridx('e')`
isalnum()| `str.isalnum()` | `str =~ '^[[:alnum:]]\+'`
isalpha()| `str.isalpha()` | `str =~ '^\a\+$'`
isdigit()| `str.isdigit()` | `str =~ '^\d\+$'`
islower()| `str.islower()` | `str =~ '^\l\+$'`
isspace()| `str.isspace()` | `str =~ '^\s\+$'`
istitle()| `str.istitle()` | `str =~ '\(\<\u\l\+\>\)\s\?'`
isupper()| `str.isupper()` | `str =~ '^\u\+$'`
join()| `':'.join(['a', 'b', 'c'])` | `join(['a', 'b', 'c'], ':')`
ljust()| `'abc'.ljust(10)` | `Not available`
lower()| `'Hello'.lower()` | `'Hello'->tolower()`
lstrip()| `'  vim  '.lstrip()` | `'  vim  '->trim(' ', 1)`
partition()| `'ab-cd-ef'.partition('-')` | `'ab-cd-ef'->matchlist('\(.\{-}\)\(-\)\(.*\)')[1:3]`
replace()| `'abc'.replace('abc', 'xyz')` | `'abc'->substitute('abc', 'xyz', 'g')`
rfind()| `'running'.rfind('ing')` | `'running'->strridx('ing')`
rindex()| `'running'.rindex('ing')` | `'running'->strridx('ing')`
rjust()| `'abc'.rjust(10)` | `Not available`
rpartition()| `'ab-cd-ef'.rpartition('-')` | `'ab-cd-ef'->matchlist('\(.*\)\(-\)\(.*\)')[1:3]`
rsplit()| `'a-b-c-d'.rsplit('-', 2)` | `Not available`
rstrip()| `'  vim  '.rstrip()` | `'  vim  '->trim(' ', 2)`
split()| `'a-b-c'.split('-')` | `'a-b-c'->split('-')`
splitlines()| `"one\ntwo".splitlines()` | `"one\ntwo"->split("\n")`
startswith()| `'running'.startswith('run')` | `'running' =~# '^run'`
strip()| `'  vim  '.strip()` | `'  vim  '->trim()`
swapcase()| `'Abc'.swapcase()` | `Not available`
title()| `'onE twO'.title()` | `'onE twO'->substitute('\<\(.\)\(\S\+\)\>', '\u\1\L\2', 'g')`
translate()| `'abcd'.translate(string.maketrans('bd', '12'))` | `'abcd'->tr('bd', '12')`
upper()| `'Hello'.upper()` | `'Hello'->toupper()`
zfill()| `str.zfill(10)` | `Not available`

*Help:* [string-functions](https://vimhelp.org/usr_41.txt.html#string-functions)

------------------------------------------------------------------------------

## List

### Creating a List

**Python:**
```python
l = [1, 2, 3, 4]
```

**VimScript:**
```vim
let l = [1, 2, 3, 4]
```

*Help:* [List](https://vimhelp.org/eval.txt.html#List)

### Accessing a List element

**Python:**
```python
l = [1, 2, 3, 4]
v1 = l[2]
v2 = l[-2]
```

**VimScript:**
```vim
let l = [1, 2, 3, 4]
let v1 = l[2]
let v2 = l[-2]
```

*Help:* [list-index](https://vimhelp.org/eval.txt.html#list-index)

### Changing the value of a List element

**Python:**
```python
l = [1, 2, 3, 4]
l[3] = 5
```

**VimScript:**
```vim
let l = [1, 2, 3, 4]
let l[3] = 5
```

*Help:* [list-modification](https://vimhelp.org/eval.txt.html#list-modification)

### Adding an item to a List

**Python:**
```python
l = []
l.append(5)
l += [6, 7]
```

**VimScript:**
```vim
let l = []
call add(l, 5)
let l += [5, 6]
```

*Help:* [add()](https://vimhelp.org/builtin.txt.html#add%28%29)

### Extending a List using another List

**Python:**
```python
l = []
l.extend([2, 3])
l += [6, 7]
```

**VimScript:**
```vim
let l = []
call extend(l, [2, 3])
let l += [6, 7]
```

*Help:* [extend()](https://vimhelp.org/builtin.txt.html#extend%28%29)

### Inserting an item in a List

**Python:**
```python
l = [1, 3]
l.insert(1, 2)
```

**VimScript:**
```vim
let l = [1, 3]
" insert before index 1
eval l->insert(2, 1)
" insert at the begining
eval l->insert(5)
```

*Help:* [insert()](https://vimhelp.org/builtin.txt.html#insert%28%29)

### Removing an item from a List

**Python:**
```python
l = [4, 5, 6]
l.remove(5)
del l[0]
```

**VimScript:**
```vim
let l = [4, 5, 6]
let idx = index(l, 5)
if idx != -1
  call remove(l, idx)
endif
unlet l[0]
```

*Help:* [remove()](https://vimhelp.org/builtin.txt.html#remove%28%29), [:unlet](https://vimhelp.org/eval.txt.html#%3aunlet)

### Removing the last element from a List

**Python:**
```python
l = [1, 2, 3]
v = l.pop()
```

**VimScript:**
```vim
let l = [1, 2, 3]
let v = l->remove(-1)
```

*Help:* [remove()](https://vimhelp.org/builtin.txt.html#remove%28%29)

### Find the index of an item in a List

**Python:**
```python
l = [1, 2, 3]
x = l.index(2)
```

**VimScript:**
```vim
let l = [1, 2, 3]
let x = l->index(2)
```

*Help:* [index()](https://vimhelp.org/builtin.txt.html#index%28%29)

### Find the index of an item in a List of Dicts by a Dict item value

**Python:**
```python
colors = [{'color': 'red'}, {'color': 'blue'}, {'color': 'green'}]
idx = next((i for i, v in enumerate(colors) if v['color'] == 'blue'), -1)
print(idx)
```

**VimScript:**
```vim
let colors = [{'color': 'red'}, {'color': 'blue'}, {'color': 'green'}]
let idx = indexof(colors, {i, v -> v.color == 'blue'})
echo idx
```

*Help:* [indexof()](https://vimhelp.org/builtin.txt.html#indexof%28%29)

### List slices

In Python, when using an index range to specify a series of items in a List, the item at the end index is not included. In Vim, the item at the end index is included.

**Python:**
```python
l = [1, 2, 3, 4]
m = l[1:3]
m = l[2:]
```

**VimScript:**
```vim
let l = [1, 2, 3, 4]
let m = l[1:3]
let m = l[2:]
let m = l[-2:]
```

*Help:* [sublist](https://vimhelp.org/eval.txt.html#sublist)

### List Concatenation

**Python:**
```python
l = [1, 2] + [3 ,4]
```

**VimScript:**
```vim
let l = [1, 2] + [3 ,4]
```

*Help:* [list-index](https://vimhelp.org/eval.txt.html#list-index)

### Adding multiple items to a List using repetition

**Python:**
```python
l = ['vim'] * 4
```

**VimScript:**
```vim
let l = ['vim']->repeat(4)
```

*Help:* [repeat()](https://vimhelp.org/builtin.txt.html#repeat%28%29)

### Count the number of occurrences of an item in a List

**Python:**
```python
l = [2, 4, 4, 5]
x = l.count(4)
```

**VimScript:**
```vim
let l = [2, 4, 4, 5]
let x = l->count(2)
```

*Help:* [count()](https://vimhelp.org/builtin.txt.html#count%28%29)

### Finding the List length

**Python:**
```python
l = ['a', 'b', 'c']
n = len(l)
```

**VimScript:**
```vim
let l = ['a', 'b', 'c']
let n = l->len()
```

*Help:* [len()](https://vimhelp.org/builtin.txt.html#len%28%29)

### List Iteration

**Python:**
```python
l = [10, 20, 30]
for v in l:
  print(v)

# Print both the list index and the value
for i, v in enumerate(l):
  print(i, v)

# Use a for loop for list iteration
for i in range(len(l)):
  print(i, l[i])
```

**VimScript:**
```vim
let l = [10, 20, 30]
for v in l
  echo v
endfor

" Print both the list index and the value
for [i, v] in items(l)
  echo i v
endfor

" Use a for loop for list iteration
for i in range(len(l))
  echo i l[i]
endfor
```

*Help:* [:for](https://vimhelp.org/eval.txt.html#%3Afor)

### Sort a List

**Python:**
```python
l = [3, 2, 1]
l.sort()
print(l)
```

**VimScript:**
```vim
let l = [3, 2, 1]
echo l->sort()
```

*Help:* [sort()](https://vimhelp.org/builtin.txt.html#sort%28%29)

### Getting unique elements in a List

**Python:**
```python
l = ['a', 'b', 'b', 'c', 'c', 'd']
# order of the elements is not retained
tset = set(l)
print(list(tset))
```

**VimScript:**
```vim
" needs a sorted list
let l = ['a', 'b', 'b', 'c', 'c', 'd']
echo copy(l)->uniq()
```

*Help:* [uniq()](https://vimhelp.org/builtin.txt.html#uniq%28%29)

### Reverse a List

**Python:**
```python
l = [1, 2, 3]
l.reverse()
print(l)
```

**VimScript:**
```vim
let l = [1, 2, 3]
echo reverse(l)
```

*Help:* [reverse()](https://vimhelp.org/builtin.txt.html#reverse%28%29)

### Copying a List

**Python:**
```python
l = [3, 2, 1]
l2 = l.copy()
```

**VimScript:**
```vim
let l = [3, 2, 1]
let l2 = l->copy()
```

*Help:* [copy()](https://vimhelp.org/builtin.txt.html#copy%28%29)

### Deep Copying a List

**Python:**
```python
import copy
a = [[1, 2], [3, 4]]
b = copy.deepcopy(a)
```

**VimScript:**
```vim
let a = [[1, 2], [3, 4]]
let b = a->deepcopy()
```

*Help:* [deepcopy()](https://vimhelp.org/builtin.txt.html#deepcopy%28%29)

### Empty a List

**Python:**
```python
l = [3, 2, 1]
l.clear()
```

**VimScript:**
```vim
let l = [3, 2, 1]
unlet l[:]
```

*Help:* [:unlet](https://vimhelp.org/eval.txt.html#%3aunlet)

### Comparing two Lists

**Python:**
```python
l1 = [1, 2]
l2 = l1
l3 = [1, 2]
if l1 is l2:
  print("Lists l1 and l2 refer to the same list")
if l1 is not l3:
  print("Lists l1 and l3 do not refer to the same list")
if l1 == l3:
  print("Lists l1 and l3 contain the same elements")
if l1 != l3:
  print("Lists l1 and l3 are different")
```

**VimScript:**
```vim
let l1 = [1, 2]
let l2 = l1
let l3 = [1, 2]
if l1 is l2
  echo "Lists l1 and l2 refer to the same list"
endif
if l1 isnot l3
  echo "Lists l1 and l2 do not refer to the same list"
endif
if l1 == l3
  echo "Lists l1 and l3 contain the same elements"
endif
if l1 != l3
  echo "Lists l1 and l3 are different"
endif
```

*Help:* [list-identity](https://vimhelp.org/eval.txt.html#list-identity)

### Filter selected elements from a List

Note that Vim does not support List comprehension.

**Python:**
```python
odd = list(filter(lambda x: x % 2, range(10)))
odd = [x for x in range(10) if x % 2]
```

**VimScript:**
```vim
let odd = filter(range(10), {idx, v -> v % 2})
```

*Help:* [filter()](https://vimhelp.org/builtin.txt.html#filter%28%29)

### Map List elements

Note that Vim does not support List comprehension.

**Python:**
```python
num_str = list(map(lambda x: str(x), range(10)))
num_str = [str(x) for x in range(10)]
```

**VimScript:**
```vim
let num_str = map(range(10), {idx, v -> string(v)})
```

*Help:* [map()](https://vimhelp.org/builtin.txt.html#map%28%29)

### Reducing List elements

**Python:**
```python
# using a lambda function
from functools import reduce
sum = reduce((lambda x, y: x + y), [1, 2, 3, 4])

# using a function
def SumNum(a, b):
    return a + b
sum = reduce(SumNum, [1, 2, 3, 4])
```

**VimScript:**
```vim
" using a lambda function
let sum = reduce([1, 2, 3, 4], {x, y -> x + y})

" using a function
func SumNum(x, y)
  return a:x + a:y
endfunc
let sum = reduce([1, 2, 3, 4], function('SumNum'))
echo sum
```

*Help:* [reduce()](https://vimhelp.org/builtin.txt.html#reduce%28%29)

### Flattening a List

**Python:**
```python
l = [[5, 9], [1, 3], [10, 20]]
flattenlist = [i for subl in l for i in subl]
print(flattenlist)
```

**VimScript:**
```vim
let l = [[5, 9], [1, 3], [10, 20]]
let flattenlist = flattennew(l)
echo flattenlist
```

*Help:* [flatten()](https://vimhelp.org/builtin.txt.html#flatten%28%29), [flattennew()](https://vimhelp.org/builtin.txt.html#flattennew%28%29)

### Finding the mix/max value in a List

**Python:**
```python
l = [3, 10, 8]
v1 = min(l)
v2 = max(l)
print(v1, v2)
```

**VimScript:**
```vim
let l = [3, 10, 8]
let v1 = l->min()
let v2 = l->max()
echo v1 v2
```

*Help:* [max()](https://vimhelp.org/builtin.txt.html#max%28%29), [min()](https://vimhelp.org/builtin.txt.html#min%28%29)

### Converting a List to a string

**Python:**
```python
s = str([3, 5, 7])
```

**VimScript:**
```vim
let s = string([3, 5, 7])
```

*Help:* [string()](https://vimhelp.org/builtin.txt.html#string%28%29)

### List Methods

Method|Python|VimScript
----|------|---------
append()| `m.append(6)` | `m->add(6)`
clear()| `m.clear()` | `unlet m[:]`
copy()| `m.copy()` | `m->copy()`
count()| `m.count(6)` | `m->count(6)`
extend()| `m.extend([5, 6])` | `m->extend([5, 6])`
index()| `m.index(6)` | `m-index(6)`
insert()| `m.insert(2, 9)` | `m->insert(9, 2)`
pop()| `m.pop()` | `m->remove(-1)`
remove()| `m.remove(6)` | `m->remove(m->index(6))`
reverse()| `m.reverse()` | `m->reverse()`
sort()| `m.sort()` | `m->sort()`

*Help:* [list-functions](https://vimhelp.org/usr_41.txt.html#list-functions)

------------------------------------------------------------------------------

## Dictionary

### Creating a Dict

**Python:**
```python
d = {'red' : 10, 'blue' : 20}
x = {}
```

**VimScript:**
```vim
let d = {'red' : 10, 'blue' : 20}
let d = #{red : 10, blue : 20}
let x = {}
```

*Help:* [Dict](https://vimhelp.org/eval.txt.html#Dict)

### Retrieving the value of a Dict item

**Python:**
```python
d = {'red' : 10, 'blue' : 20}
x = d['red']
```

**VimScript:**
```vim
let d = #{red : 10, blue : 20}
let x = d['red']
let x = d.red
```

*Help:* [dict](https://vimhelp.org/eval.txt.html#dict)

### Changing the value of a Dict item

**Python:**
```python
d = {'red' : 10, 'blue' : 20}
d['red'] = 15
```

**VimScript:**
```vim
let d = #{red : 10, blue : 20}
let d.red = 15
```

*Help:* [dict-modification](https://vimhelp.org/eval.txt.html#dict-modification)

### Accessing a Dict item

**Python:**
```python
d = {'red' : 10, 'blue' : 20}
v = d.get('red')
```

**VimScript:**
```vim
let d = {'red' : 10, 'blue' : 20}
let v = get(d, 'red')
```

*Help:* [get()](https://vimhelp.org/builtin.txt.html#get%28%29)

### Adding a new Dict item

**Python:**
```python
d = {'red' : 10, 'blue' : 20}
d['green'] = 30
```

**VimScript:**
```vim
let d = {'red' : 10, 'blue' : 20}
let d.green = 30
```

*Help:* [dict-modification](https://vimhelp.org/eval.txt.html#dict-modification)

### Extending a Dict using another Dict

**Python:**
```python
d = {}
d.update({'color' : 'grey'})
```

**VimScript:**
```vim
let d = {}
eval d->extend({'color' : 'grey'})
```

*Help:* [extend()](https://vimhelp.org/builtin.txt.html#extend%28%29)

### Removing an item from a Dict

**Python:**
```python
d = {'red' : 10, 'blue' : 20}
d.pop('red')
```

**VimScript:**
```vim
let d = {'red' : 10, 'blue' : 20}
call remove(d, 'red')
```

*Help:* [remove()](https://vimhelp.org/builtin.txt.html#remove%28%29),

### Clearing all the items from a Dict

**Python:**
```python
d = {'red' : 10, 'blue' : 20}
d.clear()
```

**VimScript:**
```vim
let d = {'red' : 10, 'blue' : 20}
call filter(d, "0")
```

*Help:* [dict-modification](https://vimhelp.org/eval.txt.html#dict-modification)

### Getting the size of a Dict

**Python:**
```python
d = {'red' : 10, 'blue' : 20}
n = len(d)
```

**VimScript:**
```vim
let d = {'red' : 10, 'blue' : 20}
let n = d->len()
```

*Help:* [len()](https://vimhelp.org/builtin.txt.html#len%28%29)

### Count the number of occurrences of a value in a Dict

**Python:**
```python
d = {'red' : 10, 'blue' : 10}
x = sum(n == 10 for n in d.values())
print(x)
```

**VimScript:**
```vim
let d = {'red' : 10, 'blue' : 10}
let x = d->count(10)
echo x
```

*Help:* [count()](https://vimhelp.org/builtin.txt.html#count%28%29)

### Checking Dict membership

**Python:**
```python
d = {'red' : 10, 'blue' : 20}
if 'red' in d:
    print("found red key")
```

**VimScript:**
```vim
let d = {'red' : 10, 'blue' : 20}
if d->has_key('red')
  echo "found red key"
endif
```

*Help:* [has_key()](https://vimhelp.org/builtin.txt.html#has_key%28%29)

### Iterating through all the keys in a Dict

**Python:**
```python
d = {'red' : 10, 'blue' : 20}
for k in d:
    print(k)
for k in d:
    print(d[k])
```

**VimScript:**
```vim
let d = {'red' : 10, 'blue' : 20}
for k in keys(d)
  echo k
endfor
for k in d->keys()
  echo d[k]
endfor
```

*Help:* [keys()](https://vimhelp.org/builtin.txt.html#keys%28%29)

### Iterating through all the values in a Dict

**Python:**
```python
d = {'red' : 10, 'blue' : 20}
for v in d.values():
    print(v)
```

**VimScript:**
```vim
let d = {'red' : 10, 'blue' : 20}
for v in d->values()
  echo v
endfor
```

*Help:* [values()](https://vimhelp.org/builtin.txt.html#values%28%29)

### Iterating through all the key, value pairs in a Dict

**Python:**
```python
d = {'red' : 10, 'blue' : 20}
for k, v in d.items():
    print(k, v)
```

**VimScript:**
```vim
let d = {'red' : 10, 'blue' : 20}
for [k, v] in d->items()
  echo k v
endfor
```

*Help:* [items()](https://vimhelp.org/builtin.txt.html#items%28%29)

### Copying a Dict

**Python:**
```python
d = {'red' : 10, 'blue' : 20}
new_d = d.copy()
```

**VimScript:**
```vim
let d = {'red' : 10, 'blue' : 20}
let new_d = d->copy()
```

*Help:* [copy()](https://vimhelp.org/builtin.txt.html#copy%28%29)

### Deep Copying a Dict

**Python:**
```python
import copy
a = {'x' : [1, 2], 'y' : [3, 4]}
b = copy.deepcopy(a)
```

**VimScript:**
```vim
let a = {'x' : [1, 2], 'y' : [3, 4]}
let b = a->deepcopy()
```

*Help:* [deepcopy()](https://vimhelp.org/builtin.txt.html#deepcopy%28%29)

### Comparing two Dicts

**Python:**
```python
d1 = {'a' : 10, 'b' : 20}
d2 = {'a' : 10, 'b' : 20}
if d1 == d2:
    print("Dicts d1 and d2 have the same content")
d3 = d1
if d1 is d3:
  print("Dicts d1 and d3 refer to the same dict")
if d2 is not d3:
  print("Dicts d2 and d3 do not refer to the same dict")
```

**VimScript:**
```vim
let d1 = {'a' : 10, 'b' : 20}
let d2 = {'a' : 10, 'b' : 20}
if d1 == d2
  echo "Dicts d1 and d2 have the same content"
endif
let d3 = d1
if d1 is d3
  echo "Dicts d1 and d3 refer to the same dict"
endif
if d2 isnot d3
  echo "Dicts d2 and d3 do not refer to the same dict"
endif
```

*Help:* [dict-identity](https://vimhelp.org/eval.txt.html#dict-identity)

### Filter selected elements from a Dict

Note that Vim does not support Dict comprehension.

**Python:**
```python
d1 = {'red' : 10, 'green' : 20, 'blue' : 10}
# filter dict items with value 10
d2 = dict(filter(lambda e : e[1] == 10, d1.items()))
print(d1, d2)
# use dict comprehension
d3 = {k: v for [k, v] in d1.items() if v == 10}
print(d1, d3)
```

**VimScript:**
```vim
let d1 = {'red' : 10, 'green' : 20, 'blue' : 10}
" filter dict items with value 10
let d2 = copy(d1)->filter({k, v -> v == 10})
echo d1 d2
```

*Help:* [filter()](https://vimhelp.org/builtin.txt.html#filter%28%29)

### Map Dict elements

Note that Vim does not support Dict comprehension.

**Python:**
```python
d1 = {'red' : 10, 'green' : 20, 'blue' : 30}
# increment the values by 5
d2 = dict(map(lambda e : (e[0], e[1] + 5), d1.items()))
print(d1, d2)
# use dict comprehension
d3 = {k: v + 5 for k, v in d1.items()}
print(d1, d3)
```

**VimScript:**
```vim
let d1 = {'red' : 10, 'green' : 20, 'blue' : 30}
" increment the values by 5
let d2 = copy(d1)->map({k, v -> v + 5})
echo d1 d2
```

*Help:* [map()](https://vimhelp.org/builtin.txt.html#map%28%29)

### Finding the min/max value in a Dict

**Python:**
```python
d = {'red' : 10, 'green' : 20}
v1 = min(d.values())
v2 = max(d.values())
print(v1, v2)
```

**VimScript:**
```vim
let d = {'red' : 10, 'green' : 20}
let v1 = d->min()
let v2 = d->max()
echo v1 v2
```

*Help:* [max()](https://vimhelp.org/builtin.txt.html#max%28%29), [min()](https://vimhelp.org/builtin.txt.html#min%28%29)

### Converting a Dict to a string

**Python:**
```python
d = str({'a' : 1, 'b' : 2})
```

**VimScript:**
```vim
let d = string({'a' : 1, 'b' : 2})
```

*Help:* [string()](https://vimhelp.org/builtin.txt.html#string%28%29)

### Dictionary Methods

Method|Python|VimScript
----|------|---------
clear()| `d.clear()` | `call filter(d, '0')`
copy()| `newDict = d.copy()` | `let newDict = d->copy()`
fromkeys()| `d = dict.fromkeys(x)` | `Not available`
get()| `v = d.get('red')` | `let v = d->get('red')`
in or has_key() | `'red' in d` | `d->has_key('red')`
items()| `d.items()` | `d->items()`
keys()| `d.keys()` | `d->keys()`
pop()| `d.pop('red')` | `call d->remove('red')`
popitem()| `d.popitem()` | `Not available`
setdefault()| `d.setdefault('red', 10)` | `Not available`
update()| `d.update({'a' : 10, 'b' : 20}` | `d->extend({'a' : 10, 'b' : 20})`
values()| `d.values()` | `d->values()`

*Help:* [dict-functions](https://vimhelp.org/usr_41.txt.html#dict-functions)

------------------------------------------------------------------------------

## If statement

### Basic If statement

**Python:**
```python
if a > b:
    print("a is greater than b")
```

**VimScript:**
```vim
if a > b
  echo "a is greater than b"
endif
```

*Help:* [:if](https://vimhelp.org/eval.txt.html#%3aif)

### If-else statement

**Python:**
```python
if a > b:
    print("a is greater than b")
else:
    print("a is less than or equal to b")
```

**VimScript:**
```vim
if a > b
  echo "a is greater than b"
else
  echo "a is less than or equal to b"
endif
```

*Help:* [:else](https://vimhelp.org/eval.txt.html#%3aelse)

### If-elseif statement

**Python:**
```python
if a > b:
    print("a is greater than b")
elif a < b:
    print("a is less than b")
else:
    print("a is equal to b")
```

**VimScript:**
```vim
if a > b
  echo "a is greater than b"
elseif a < b
  echo "a is less than b"
else
  echo "a is equal to b"
endif
```

*Help:* [:elseif](https://vimhelp.org/eval.txt.html#%3aelseif)

### Checking multiple conditions

**Python:**
```python
if a > b and (a > c or a > d):
    print "a is greater than b and greater than c or d"
```

**VimScript:**
```vim
if a > b && (a > c || a > d)
  echo "a is greater than b and greater than c or d"
endif
```

*Help:* [expr2](https://vimhelp.org/eval.txt.html#expr2)

### Nested If statements
**Python:**
```python
if status == True:
    if a >= 1:
        print("Nested if")
```

**VimScript:**
```vim
if status == v:true
  if a >= 1
    echo "Nested if"
  endif
endif
```

*Help:* [:if](https://vimhelp.org/eval.txt.html#%3aif)

------------------------------------------------------------------------------

## For Loop

**Python:**
```python
for i in range(5):
    print(i)
```

**VimScript:**
```vim
for i in range(5)
  echo i
endfor
```

*Help:* [:for](https://vimhelp.org/eval.txt.html#%3afor)

### Breaking out of a For loop
**Python:**
```python
for i in ['a', 'b', 'c']:
    if i == 'b':
        break
    print(i)
```

**VimScript:**
```vim
for i in ['a', 'b', 'c']
  if i == 'b'
    break
  endif
  echo i
endfor
```

*Help:* [:break](https://vimhelp.org/eval.txt.html#%3abreak)

### Continuing a For loop
**Python:**
```python
for i in ['a', 'b', 'c']:
    if i == 'b':
        continue
    print(i)
```

**VimScript:**
```vim
for i in ['a', 'b', 'c']
  if i == 'b'
    continue
  endif
  echo i
endfor
```

*Help:* [:continue](https://vimhelp.org/eval.txt.html#%3acontinue)

### Nested For Loops
**Python:**
```python
for i in range(10):
    for j in range(10):
        print(str(i) + 'x' + str(j) + '=' + str(i * j))
```

**VimScript:**
```vim
for i in range(4)
  for j in range(4)
    echo i .. 'x' .. j .. '=' .. i * j
  endfor
endfor
```

*Help:* [:for](https://vimhelp.org/eval.txt.html#%3afor)

------------------------------------------------------------------------------

## While Loop

**Python:**
```python
i = 1
while i <= 5 :
   print(i)
   i += 1
```

**VimScript:**
```vim
let i = 1
while i <= 5
  echo i
  let i += 1
endwhile
```

*Help:* [:while](https://vimhelp.org/eval.txt.html#%3awhile)

------------------------------------------------------------------------------

## Comment

**Python:**
```python
# This is a python comment
i = 0    # First iteration
```

**VimScript:**
```vim
" This is a Vimscript comment
let i = 0    " First iteration
```

*Help:* [:comment](https://vimhelp.org/cmdline.txt.html#%3acomment)

------------------------------------------------------------------------------

## Function

The names of global functions in Vim must start with an uppercase letter.
The examples in this document use the "function" and "endfunction" keywords for
defining a function. But these can be abbreviated as "func" and "endfunc".

When a function encounters an error, it will continue to execute the rest of the function unless the "abort" argument is specified when defining the function. So it is recommended to specify the "abort" keyword at the end of the "function" line. To simplify the example code, this keyword is not used in this guide.

### Defining a Function

**Python:**
```python
def Min(x, y):
    return x if < y else y

print(Min(6, 3)
```

**VimScript:**
```vim
function Min(x, y) abort
  return a:x < a:y ? a:x : a:y
endfunction

echo Min(6, 3)
```

*Help:* [user-functions](https://vimhelp.org/eval.txt.html#user-functions)

### Calling a Function

**Python:**
```python
def EchoValue(v):
    print(v)

EchoValue(100)
```

**VimScript:**
```vim
function EchoValue(v)
  echo a:v
endfunction

call EchoValue(100)
```

*Help:* [:call](https://vimhelp.org/eval.txt.html#%3acall)

### Function return value

**Python:**
```python
def Sum(a, b):
    return a + b

s = Sum(10, 20)
```

**VimScript:**
```vim
function Sum(a, b)
  return a:a + a:b
endfunction

let s = Sum(10, 20)
```

*Help:* [:return](https://vimhelp.org/eval.txt.html#%3areturn)

### Pass by reference

**Python:**
```python
def AddValues(l):
    l.extend([1, 2, 3, 4])
```

**VimScript:**
```vim
function AddValues(l)
  call extend(a:l, [1, 2, 3, 4])
endfunction
```

*Help:* [function-argument](https://vimhelp.org/eval.txt.html#function-argument)

### Variable number of arguments

**Python:**
```python
def Sum(v1, *args):
    sum = v1
    for i in *args:
        sum += i
    return sum
```

**VimScript:**
```vim
function Sum(v1, ...)
  let sum = a:v1
  for i in a:000
    let sum +=i
  endfor
  return sum
endfunction
let s1 = Sum(10, 20, 30, 40)
let s1 = Sum(10)
```

*Help:* [a:000](https://vimhelp.org/eval.txt.html#a%3a000)

### Default value for arguments

**Python:**
```python
def Powerof(base, exp = 2):
    return base ** exp
```

**VimScript:**
```vim
function PowerOf(base, exp = 2)
  return float2nr(pow(a:base, a:exp))
endfunction
let val = PowerOf(4)
let val = PowerOf(4, 3)
```

*Help:* [optional-function-argument](https://vimhelp.org/eval.txt.html#optional-function-argument)

### Accessing global variables

**Python:**
```python
counter = 1
def Incr():
    global counter
    counter += 1
```

**VimScript:**
```vim
let counter = 1
function Incr()
  let g:counter += 1
endfunction
cal Incr()
```

*Help:* [global-variable](https://vimhelp.org/eval.txt.html#global-variable)

### Function reference

**Python:**
```python
def Foo():
    print("Foo")
Bar = Foo
Bar()
```

**VimScript:**
```vim
function Foo()
  echo "Foo"
endfunction
let Bar = function("Foo")
call Bar()
```

*Help:* [Funcref](https://vimhelp.org/eval.txt.html#Funcref)

------------------------------------------------------------------------------

## Lambda Function

**Python:**
```python
F = lambda x , y: x - y
print(F(5,2))
```

**VimScript:**
```vim
let F = {x, y -> x - y}
echo F(5,2)
```

*Help:* [lambda](https://vimhelp.org/eval.txt.html#lambda)

------------------------------------------------------------------------------

## Partial Function

**Python:**
```python
import functools
def Mylog(subsys, msg):
    print("%s: %s" % (subsys, msg))

ErrLog = functools.partial(Mylog, 'ERR')
ErrLog("Failed to open file")
```

**VimScript:**
```vim
function Mylog(subsys, msg)
  echo printf("%s: %s", a:subsys, a:msg)
endfunction

let ErrLog = function('Mylog', ['ERR'])
call ErrLog("Failed to open file")
```

*Help:* [Partial](https://vimhelp.org/eval.txt.html#Partial)

------------------------------------------------------------------------------

## Closure

### Closure with a lambda function

**Python:**
```python
def foo(arg):
  i = 3
  return lambda x: x + i - arg

bar = foo(4)
print(bar(6))
```

**VimScript:**
```vim
function Foo(arg)
  let i = 3
  return {x -> x + i - a:arg}
endfunction

let Bar = Foo(4)
echo Bar(6)
```

*Help:* [closure](https://vimhelp.org/eval.txt.html#closure)

### Closure with a function reference

**Python:**
```python
def Foo(base):
    def Bar(val):
        return base + val
    return Bar

F = Foo(10)
print(F(2))
F = Foo(20)
print(F(2))
```

**VimScript:**
```vim
function Foo(base)
  function Bar(val) closure
    return a:base + a:val
  endfunction
  return funcref('Bar')
endfunction

let F = Foo(10)
echo F(2)
let F = Foo(20)
echo F(2)
```

*Help:* [func-closure](https://vimhelp.org/eval.txt.html#%3Afunc-closure)

------------------------------------------------------------------------------

## Class

### Defining a class

**Python:**
```python
class Point:
    def __init__(self, x, y):
        self.x = x
        self.y = y

    def getX(self):
        return self.x

    def getY(self):
        return self.y

    def setX(self, x):
        self.x = x

    def setY(self, y):
        self.y = y

    def Print(self):
        print("Pt = (" + str(self.x) + ", " + str(self.y) + ")")

pt = Point(10, 20)
pt.setX(40)
pt.setY(50)
pt.Print()
```

**VimScript:**
```vim
function s:new(x, y) dict
  let newpt = copy(self)
  let newpt.x = a:x
  let newpt.y = a:y
  return newpt
endfunction

function s:getX() dict
  return self.x
endfunction

function s:getY() dict
  return self.y
endfunction

function s:setX(x) dict
  let self.x = a:x
endfunction

function s:setY(y) dict
  let self.y = a:y
endfunction

function s:Print() dict
  echo "Pt = (" .. self.x .. ", " .. self.y .. ")"
endfunction

let Point = {}
let Point.new = function("s:new")
let Point.getX = function("s:getX")
let Point.getY = function("s:getY")
let Point.setX = function("s:setX")
let Point.setY = function("s:setY")
let Point.Print = function("s:Print")

let p = Point.new(10, 20)
call p.setX(40)
call p.setY(50)
call p.Print()
```

*Help:* [Dictionary-function](https://vimhelp.org/eval.txt.html#Dictionary-function), [numbered-function](https://vimhelp.org/eval.txt.html#numbered-function), [:func-dict](https://vimhelp.org/eval.txt.html#%3afunc-dict)

------------------------------------------------------------------------------

## Exception Handling

### Basic exception handling

**Python:**
```python
try:
    f = open('buf.java', 'r')
    lines = f.readlines()
    f.close()
except IOError:
    print("Failed to open file")
```

**VimScript:**
```vim
try
  let l = readfile('buf.java')
catch /E484:/
  echo "Failed to read file"
endtry
```

*Help:* [exception-handling](https://vimhelp.org/eval.txt.html#exception-handling)

### Catching all exceptions

**Python:**
```python
try:
    f = open('buf.java', 'r')
    lines = f.readlines()
    f.close()
except Exception as e:
    print("Caught " + str(e))
```

**VimScript:**
```vim
try
  let l = readfile('buf.java')
catch
  echo "Caught " .. v:exception
endtry
```

*Help:* [catch-errors](https://vimhelp.org/eval.txt.html#catch-errors)

### Executing code after a try block (finally)

**Python:**
```python
try:
    f = open('buf.java', 'r')
    lines = f.readlines()
    f.close()
finally:
    print("executing code in finally block")
```

**VimScript:**
```vim
try
  let l = readfile('buf.java')
finally
  echo "executing code in finally block"
endtry
```

*Help:* [try-finally](https://vimhelp.org/eval.txt.html#try-finally)

### Raising a custom exception

**Python:**
```python
try:
    raise Exception('MyException')
except Exception as e:
    if str(e) == 'MyException':
        print("Caught MyException")
finally:
    print("Finally block")
```

**VimScript:**
```vim
try
  throw 'MyException'
catch /MyException/
  echo "Caught MyException"
finally
  echo "Finally block"
endtry
```

*Help:* [throw-catch](https://vimhelp.org/eval.txt.html#throw-catch)

------------------------------------------------------------------------------

## Line Continuation

**Python:**
```python
a = 1 + 2 + 3 + \
      4 + 5 + 6
```

**VimScript:**
```vim
let a = 1 + 2 + 3 +
      \ 4 + 5 + 6
```

*Help:* [line-continuation](https://vimhelp.org/eval.txt.html#line-continuation)

------------------------------------------------------------------------------

## File Operations

### Reading all the lines from a file

Vim has a function to read the entire file but doesn't have a function to read a file one line at a time.

**Python:**
```python
with open('myfile.txt', 'r') as f:
    lines = f.readlines()
# lines == ['line1\n', 'line2\n'] 
```

**VimScript:**
```vim
let lines = readfile("myfile.txt")
" lines == ['line1', 'line2'] 
```

*Help:* [readfile()](https://vimhelp.org/builtin.txt.html#readfile%28%29)

### Writing lines to a file
**Python:**
```python
lines = ['line1\n', 'line2\n']
with open('myfile.txt', 'w') as fh:
    fh.writelines(lines)
    
lines = ['line1', 'line2']
with open('myfile.txt', 'w') as fh:
    print(*lines, sep='\n', file=fh)
```

**VimScript:**
```vim
call writefile(['line1', 'line2'], 'myfile.txt')
```

*Help:* [writefile()](https://vimhelp.org/builtin.txt.html#writefile%28%29)

### Appending lines to a file
**Python:**
```python
lines = ["line3\n", "line4\n"]
with open('myfile.txt', 'a') as fh:
    fh.writelines(lines)
```

**VimScript:**
```vim
call writefile(['line3', 'line4'], 'myfile.txt', 'a')
```

*Help:* [writefile()](https://vimhelp.org/builtin.txt.html#writefile%28%29)

### Checking whether a file exists
**Python:**
```python
import os.path
if os.path.isfile('myfile.txt'):
    print("File exists")
```

**VimScript:**
```vim
if filereadable('myfile.txt')
  echo "File is readable"
endif
```

*Help:* [filereadable()](https://vimhelp.org/builtin.txt.html#filereadable%28%29)

### Deleting a file
**Python:**
```python
import os
os.remove('myfile.txt')
```

**VimScript:**
```vim
call delete('myfile.txt')
```

*Help:* [remove()](https://vimhelp.org/builtin.txt.html#remove%28%29)

### Renaming a file
**Python:**
```python
import os
os.rename('myfile.txt', 'somefile.txt)
```

**VimScript:**
```vim
call rename('myfile.txt', 'somefile.txt')
```

*Help:* [rename()](https://vimhelp.org/builtin.txt.html#rename%28%29)

### Getting the size of a file
**Python:**
```python
import os
sz = os.path.getsize('move.py')
```

**VimScript:**
```vim
let sz = getfsize('move.py')
```

*Help:* [getfsize()](https://vimhelp.org/builtin.txt.html#getfsize%28%29)

------------------------------------------------------------------------------

## Directory Operations

### Creating a directory
**Python:**
```python
os.mkdir('somedir')
```

**VimScript:**
```vim
call mkdir('somedir')
```

*Help:* [mkdir()](https://vimhelp.org/builtin.txt.html#mkdir%28%29)

### Changing to a directory
**Python:**
```python
os.chdir('someotherdir')
```

**VimScript:**
```vim
call chdir('someotherdir')
```

*Help:* [chdir()](https://vimhelp.org/builtin.txt.html#chdir%28%29)

### Getting the current directory
**Python:**
```python
dir = os.getcwd()
```

**VimScript:**
```vim
let dir = getcwd()
```

*Help:* [getcwd()](https://vimhelp.org/builtin.txt.html#getcwd%28%29)

### Deleting a directory
**Python:**
```python
os.rmdir('somedir')
```

**VimScript:**
```vim
call delete('somedir', 'd')
```

*Help:* [delete()](https://vimhelp.org/builtin.txt.html#delete%28%29)

------------------------------------------------------------------------------

## Random numbers

### Generating a random number
**Python:**
```python
import random
r = random.randint(0, 2147483647)
```

**VimScript:**
```vim
let r = rand()
```

*Help:* [rand()](https://vimhelp.org/builtin.txt.html#rand%28%29)

### Generating a random number from a seed
**Python:**
```python
import random
random.seed()
r = random.randint(0, 2147483647)
print(r)
```

**VimScript:**
```vim
let seed = srand()
let r = rand(seed)
```

*Help:* [srand()](https://vimhelp.org/builtin.txt.html#srand%28%29)

------------------------------------------------------------------------------

## Mathematical Functions

Function|Python|VimScript
----|------|---------
abs| `f = math.fabs(-10)` | `let f = abs(-10)`
acos| `f = math.acos(0.8)` | `let f = acos(0.8)`
asin| `f = math.asin(0.8)` | `let f = asin(0.8)`
atan| `f = math.atan(0.8)` | `let f = atan(0.8)`
atan2| `f = math.atan2(0.4, 0.8)` | `let f = atan2(0.4, 0.8)`
ceil| `f = math.ceil(1.2)` | `let f = ceil(1.2)`
cos| `f = math.cos(4)` | `let f = cos(4)`
cosh| `f = math.cosh(4)` | `let f = cosh(4)`
exp| `f = math.exp(2)` | `let f = exp(2)`
floor| `f = math.floor(1.4)` | `let f = floor(1.4)`
log| `f = math.log(12)` | `let f = log(12)`
log10| `f = math.log10(100)` | `let f = log10(100)`
mod| `f = math.fmod(4, 3)` | `let f = fmod(4, 3)`
pow| `f = math.pow(2, 3)` | `let f = pow(2, 3)`
sin| `f = math.sin(4)` | `let f = sin(4)`
sinh| `f = math.sinh(4)` | `let f = sinh(4)`
sqrt| `f = math.sqrt(9)` | `let f = sqrt(9)`
tan| `f = math.tan(4)` | `let f = tan(4)`
tanh| `f = math.tanh(4)` | `let f = tanh(4)`
trunc| `f = math.trunc(1.3)` | `let f = trunc(1.3)`

*Help:* [ceil()](https://vimhelp.org/builtin.txt.html#ceil%28%29), [abs()](https://vimhelp.org/builtin.txt.html#abs%28%29), [floor()](https://vimhelp.org/builtin.txt.html#floor%28%29), [fmod()](https://vimhelp.org/builtin.txt.html#fmod%28%29), [trunc()](https://vimhelp.org/builtin.txt.html#trunc%28%29), [exp()](https://vimhelp.org/builtin.txt.html#exp%28%29), [log()](https://vimhelp.org/builtin.txt.html#log%28%29), [log10()](https://vimhelp.org/builtin.txt.html#log10%28%29), [pow()](https://vimhelp.org/builtin.txt.html#pow%28%29), [sqrt()](https://vimhelp.org/builtin.txt.html#sqrt%28%29), [cos()](https://vimhelp.org/builtin.txt.html#cos%28%29), [sin()](https://vimhelp.org/builtin.txt.html#sin%28%29), [tan()](https://vimhelp.org/builtin.txt.html#tan%28%29), [cosh()](https://vimhelp.org/builtin.txt.html#cosh%28%29), [sinh()](https://vimhelp.org/builtin.txt.html#sinh%28%29), [tanh()](https://vimhelp.org/builtin.txt.html#tanh%28%29), [acos()](https://vimhelp.org/builtin.txt.html#acos%28%29), [asin()](https://vimhelp.org/builtin.txt.html#asin%28%29), [atan()](https://vimhelp.org/builtin.txt.html#atan%28%29), [atan2()](https://vimhelp.org/builtin.txt.html#atan2%28%29)

------------------------------------------------------------------------------

## Date/Time functions

### Get current date and time

**Python:**
```python
from datetime import datetime
d = datetime.now()
print(d.strftime("%c"))
```

**VimScript:**
```vim
echo strftime("%c")
```

*Help:* [strftime()](https://vimhelp.org/builtin.txt.html#strftime%28%29)

### Parse a date/time string

**Python:**
```python
from datetime import datetime
print(datetime.strptime("1997 Apr 27 11:49:23", "%Y %b %d %X"))
```

**VimScript:**
```vim
echo strptime("%Y %b %d %X", "1997 Apr 27 11:49:23")
```

*Help:* [strptime()](https://vimhelp.org/builtin.txt.html#strptime%28%29)

### Getting the time in seconds since epoch

**Python:**
```python
import time
print int(time.time())
```

**VimScript:**
```vim
echo localtime()
```

*Help:* [localtime()](https://vimhelp.org/builtin.txt.html#localtime%28%29)

### Measuring elapsed time

**Python:**
```python
import time
start_time = time.perf_counter()
sum = 1
for i in range(1000):
    sum += i
end_time = time.perf_counter()
print("Elapsed time " + str(end_time - start_time))
```

**VimScript:**
```vim
let start = reltime()
let sum = 0
for i in range(1000)
  let sum += i
endfor
let elapsed_time = reltime(start)
echo "Elasped time" reltimefloat(elapsed_time)
```

*Help:* [reltime()](https://vimhelp.org/builtin.txt.html#reltime%28%29), [reltimestr()](https://vimhelp.org/builtin.txt.html#reltimestr%28%29), [reltimefloat()](https://vimhelp.org/builtin.txt.html#reltimefloat%28%29)

------------------------------------------------------------------------------

## External commands

### Getting the output of an external command as a string

**Python:**
```python
import subprocess
procObj = subprocess.Popen('grep class *.java',
    stdout=subprocess.PIPE,
    shell=True)
lines, err = procObj.communicate()
print(lines)
print("Error = " + str(procObj.returncode))
```

**VimScript:**
```vim
let lines = system('grep class *.java')
echo lines
echo "Error = " .. v:shell_error
```

*Help:* [system()](https://vimhelp.org/builtin.txt.html#system%28%29), [v:shell_error](https://vimhelp.org/eval.txt.html#v%3ashell_error)

### Splitting the output of an external command into lines

**Python:**
```python
import subprocess
procObj = subprocess.Popen('grep class *.java',
    stdout=subprocess.PIPE,
    shell=True)
lines, err = procObj.communicate()
print("Number of matches = " + str(len(lines.splitlines())))
```

**VimScript:**
```vim
let lines = systemlist('grep class *.java')
echo "Number of matches = " .. len(lines)
```

*Help:* [systemlist()](https://vimhelp.org/builtin.txt.html#systemlist%28%29)

### Sending input to an external command and getting the output

**Python:**
```python
import subprocess
procObj = subprocess.Popen('wc',
    stdout=subprocess.PIPE,
    stdin=subprocess.PIPE,
    shell=True)
lines, err = procObj.communicate("one\ntwo\n")
print(lines)
print("Error = " + str(procObj.returncode))
```

**VimScript:**
```vim
let lines = system('wc', "one\ntwo\n")
echo lines
echo "Error = " .. v:shell_error
```

*Help:* [system()](https://vimhelp.org/builtin.txt.html#system%28%29)

------------------------------------------------------------------------------

## User Input/Output

### Getting input from the user

**Python:**
```python
choice = input("coffee or tea? ")
print("You selected " + choice)
```

**VimScript:**
```vim
let ans = input("coffee or tea? ", "tea")
echo "You selected " .. ans
```

*Help:* [input()](https://vimhelp.org/builtin.txt.html#input%28%29), [inputlist()](https://vimhelp.org/builtin.txt.html#inputlist%28%29), [inputdialog()](https://vimhelp.org/builtin.txt.html#inputdialog%28%29)

### Getting password from the user

**Python:**
```python
import getpass
passwd = getpass.getpass("Password: ")
print("You entered " + passwd)
```

**VimScript:**
```vim
let passwd = inputsecret("Password: ")
echo "You entered " .. passwd
```

*Help:* [inputsecret()](https://vimhelp.org/builtin.txt.html#inputsecret%28%29)

### Print an expression

**Python:**
```python
print("Hello World\n")
s = "vim"
print("Editor = %s" % (s))
```

**VimScript:**
```vim
echo "Hello World"
let s = "vim"
echo "Editor = " .. s
```

*Help:* [:echo](https://vimhelp.org/eval.txt.html#%3Aecho), [:echon](https://vimhelp.org/eval.txt.html#%3Aechon), [echoraw()](https://vimhelp.org/builtin.txt.html#echoraw%28%29), [:echohl](https://vimhelp.org/eval.txt.html#%3Aechohl), [:echoerr](https://vimhelp.org/eval.txt.html#%3Aechoerr), [:echomsg](https://vimhelp.org/eval.txt.html#%3Aechomsg)

### Formatted Output

**Python:**
```python
name = "John"
id = 1001
print(f"Name: {name}, ID: {id}")
print("Name: {}, ID: {}".format(name, id))
print("Name: %s, ID: %d" % (name, id))
```

**VimScript:**
```vim
let name = "John"
let id = 1001
echo printf("Name: %s, ID: %d", name, id)
```

*Help:* [printf()](https://vimhelp.org/builtin.txt.html#printf%28%29)

------------------------------------------------------------------------------

## Environment Variables

### Getting the value of an environment variable

**Python:**
```python
import os
h = os.environ.get('HOME', '')
if h == '':
    print("HOME is not set")
else:
    print("HOME = " + h)
```

**VimScript:**
```vim
let h = getenv('HOME')
if h == v:null
  echo 'HOME is not set'
else
  echo 'HOME = ' .. h
endif

if !exists('$HOME')
  echo 'HOME is not set'
else
  echo 'HOME = ' .. $HOME
endif
```

*Help:* [getenv()](https://vimhelp.org/builtin.txt.html#getenv%28%29), [expr-env](https://vimhelp.org/eval.txt.html#expr-env), [exists()](https://vimhelp.org/builtin.txt.html#exists%28%29)

### Setting an environment variable

**Python:**
```python
import os
os.environ['FOO'] = "BAR"
```

**VimScript:**
```vim
call setenv('FOO', 'BAR')

let $FOO = 'BAR'
```

*Help:* [setenv()](https://vimhelp.org/builtin.txt.html#setenv%28%29), [:let-environment](https://vimhelp.org/eval.txt.html#%3alet-environment)

### Removing an environment variable

**Python:**
```python
import os
del os.environ['FOO']
```

**VimScript:**
```vim
call setenv('FOO', v:null)

unlet $FOO
```

*Help:* [setenv()](https://vimhelp.org/builtin.txt.html#setenv%28%29), [:unlet-environment](https://vimhelp.org/eval.txt.html#%3aunlet-environment)

### Getting all the environment variables

**Python:**
```python
import os
print(os.environ)
```

**VimScript:**
```vim
echo environ()
```

*Help:* [environ()](https://vimhelp.org/builtin.txt.html#environ%28%29)

------------------------------------------------------------------------------

## Command-line Arguments

### Displaying the command-line arguments

**Python:**
```python
import sys
print("Number of arguments = " + str(len(sys.argv)))
print("Arguments = " + str(sys.argv))
for arg in sys.argv:
    print(arg)
```

**VimScript:**
```vim
echo "Number of arguments = " .. len(v:argv)
echo "Arguments = " .. string(v:argv)
for arg in v:argv
  echo arg
endfor
```

*Help:* [v:argv](https://vimhelp.org/eval.txt.html#v%3aargv)

------------------------------------------------------------------------------

## Regular Expressions

### Finding whether a pattern matches a string

**Python:**
```python
import re
s = 'Test failed with error E123:'
if re.search(r'E\d+:', s):
    print('Error code found')

s = 'Test successful'
if re.search(r'E\d+:', s) is None:
    print("Test passed")
```

**VimScript:**
```vim
let s = 'Test failed with error E123:'
if s =~# 'E\d\+:'
  echo "Error code found"
endif

let s = 'Test successful'
if s !~# 'E\d\+:'
  echo "Test passed"
endif
```

*Help:* [expr-=~](https://vimhelp.org/eval.txt.html#expr-%3d~), [expr-!~](https://vimhelp.org/eval.txt.html#expr-%21~)

### Finding the beginning or ending index of a pattern in a string

**Python:**
```python
import re
m = re.search(r'\d+', "Abc 123 Def")
if m is not None:
    idx = m.start()
    end_idx = m.end()
```

**VimScript:**
```vim
let idx = match("Abc 123 Def", '\d\+')
let end_idx = matchend("Abc 123 Def", '\d\+')

let l = matchstrpos("Abc 123 Def", '\d\+')
echo "start:" l[1] "end:" l[2]
```

*Help:* [match()](https://vimhelp.org/builtin.txt.html#match%28%29), [matchend()](https://vimhelp.org/builtin.txt.html#matchend%28%29), [matchstrpos()](https://vimhelp.org/builtin.txt.html#matchstrpos%28%29)

### Getting matching substring using a pattern

**Python:**
```python
import re
m = re.search(r'\d+', "Abc 123 Def")
if m is not None:
    s = m.group(0)
    print s
```

**VimScript:**
```vim
let s = matchstr("Abc 123 Def", '\d\+')
```

*Help:* [matchstr()](https://vimhelp.org/builtin.txt.html#matchstr%28%29)

### Getting multiple matches using a pattern

**Python:**
```python
import re
m = re.match(r'(\w+) (\w+) (\w+)', "foo bar baz")
if m is not None:
    print("Full match: " + m.group(0))
    print("1: " + m.group(1) + " 2: " + m.group(2) + " 3: " + m.group(3))
```

**VimScript:**
```vim
let list = matchlist("foo bar baz", '\(\w\+\) \(\w\+\) \(\w\+\)')
echo "Full match:" list[0]
echo "1:" list[1] "2:" list[2] "3:" list[3]
```

*Help:* [matchlist()](https://vimhelp.org/builtin.txt.html#matchlist%28%29)

### Substituting text using a pattern

**Python:**
```python
import re
s = re.sub(r'bar', r'baz', "foo bar")
print(s)
```

**VimScript:**
```vim
let s = substitute("foo bar", 'bar', 'baz', '')
echo s
```

*Help:* [substitute()](https://vimhelp.org/builtin.txt.html#substitute%28%29)

### Using a function to get the replacement string

**Python:**
```python
import re
def Dashrepl(m):
  if m.group(0) == '-':
    return ' '
  else:
    return '-'
s = re.sub('-{1,2}', Dashrepl, 'pro----gram-files')
print(s)
```

**VimScript:**
```vim
function Dashrepl(m)
  if a:m[0] == '-'
    return ' '
  else
    return '-'
  endif
endfunction
let s = substitute("pro----gram-files", '-\{1,2}', function('Dashrepl'), 'g')
echo s
```

*Help:* [substitute()](https://vimhelp.org/builtin.txt.html#substitute%28%29)

### Regular expression comparison

Note that the below table contains only the regular expressions that are present in both Python and Vim.

What|Python|Vim
----|------|---
single character| `.` | `.`
start of string| `^` | `^`
end of string| `$` | `$`
0 or more matches| `*` | `*`
1 or more matches| `+` | `\+`
0 or 1 match| `?` | `\?`
non-greedy match| `*?` | `\{-}`
fixed matches| `{n}` | `\{n}`
m to n matches| `{m,n}` | `\{m,n}`
m to n non-greedy matches| `{m,n}?` | `\{-m,n}`
character class| `[...]` | `[...]`
negated character class| `[^...]` | `[^...]`
range| `[a-z]` | `[a-z]`
either-or branch| `\|` | `\\|`
capturing group| `(...)` | `\(...\)`
non-capturing match| `(?:...)` | `\%(...\)`
positive look-ahead| `(?=...)` | `\(...\)\@=`
negative look-ahead| `(?!...)` | `\(...\)\@!`
positive look-behind| `(?<=...)` | `\(...\)\@<=`
negative look-behind| `(?<!...)` | `\(...\)\@<!`
start of word| `\b` | `\<`
end of word| `\b` | `\>`
digit| `\d` | `\d`
non-digit| `\D` | `\D`
whitespace| `\s` | `\s`
non-whitespace| `\S` | `\S`
word character| `\w` | `\w`
non-word character| `\W` | `\W`
ignore case| `(?i)` | `\c`

*Help:* [pattern](https://vimhelp.org/pattern.txt.html#pattern)

------------------------------------------------------------------------------

## Binary Data

### Storing binary data in a variable

**Python:**
```python
data = bytearray(b'\x12\xF6\xAB\xFF\x49\xC0\x88\x3A\xE2\xC1\x42\xAA')
print(data)
print(data[0:4])
```

**VimScript:**
```vim
let data = 0z12F6ABFF.49C0883A.E2C142AA
echo data
echo data[0:3]
```

*Help:* [blob](https://vimhelp.org/eval.txt.html#blob)

### Manipulating binary data stored in a variable

**Python:**
```python
data = bytearray(b'')
data.append(0xC2)
data += b'\xB3\xF7\xA5'
del data[2]
print(data)
```

**VimScript:**
```vim
let data = 0z
let data[0] = 0xC2
let data += 0zB3F7A5
call remove(data, 2)
echo data
```

*Help:* [blob-index](https://vimhelp.org/eval.txt.html#blob-index), [blob-modification](https://vimhelp.org/eval.txt.html#blob-modification)

### Converting a List of numbers to binary data and vice versa

**Python:**
```python
l = [11, 12, 14]
data = bytearray(l)
print(data)

data = bytearray(b'\xDE\xAD\xBE\xEF')
l = list(data)
print(l)
```

**VimScript:**
```vim
let l = [11, 12, 14]
let data = list2blob(l)
echo data

let data = 0zDEADBEEF
let  l = blob2list(data)
echo l
```

*Help:* [list2blob()](https://vimhelp.org/builtin.txt.html#list2blob%28%29), [blob2list()](https://vimhelp.org/builtin.txt.html#blob2list%28%29)

### Reading and writing binary data from a file

**Python:**
```python
with open("datafile.bin", "rb") as bin_fh:
    data = bytearray(bin_fh.read())

with open("data2.bin", "wb") as bin_fh:
    bin_fh.write(data)
```

**VimScript:**
```vim
let data = readfile('datafile.bin', 'B')
call writefile(data, 'data2.bin')
```

*Help:* [readfile()](https://vimhelp.org/builtin.txt.html#readfile%28%29), [writefile()](https://vimhelp.org/builtin.txt.html#writefile%28%29)

------------------------------------------------------------------------------

## Timers

### One-shot Timer

**Python:**
```python
import threading
def TimerCallback(ctx):
    print("Timer callback, context = " + ctx)

timer = threading.Timer(5, TimerCallback, args=["green"])
timer.start()
```

**VimScript:**
```vim
func TimerCallback(ctx, timer_id)
  echo "Timer callback, context = " .. a:ctx .. ", id = " .. a:timer_id
endfunc

" Run a function after 5 seconds
let timer_id = timer_start(5 * 1000, function('TimerCallback', ["green"]))
```

*Help:* [timer_start()](https://vimhelp.org/builtin.txt.html#timer_start%28%29)

### Periodic Timer

**Python:**
```python
import threading
def TimerCallback():
    print("Timer callback")
    threading.Timer(5, TimerCallback).start()

# run a function every 5 seconds (approximately)
timer = threading.Timer(5, TimerCallback)
timer.start()
```

**VimScript:**
```vim
func TimerCallback(timer_id)
  echo "Timer callback"
endfunc

" run a function every 5 seconds periodically
let timer_id = timer_start(5 * 1000, function('TimerCallback'), {'repeat' : -1})
```

*Help:* [timer](https://vimhelp.org/eval.txt.html#timer)

### Stopping a timer

**Python:**
```python
import threading
def TimerCallback():
    print("Timer callback")

timer = threading.Timer(1, TimerCallback)
timer.start()
timer.cancel()
```

**VimScript:**
```vim
func TimerCallback(timer_id)
  echo "Timer callback"
endfunc

" start a timer and then stop it
let timer_id = timer_start(1000, 'TimerCallback')
call timer_stop(timer_id)

" to stop all the timers
call timer_stopall()
```

*Help:* [timer_start()](https://vimhelp.org/builtin.txt.html#timer_start%28%29)

### Sleeping for a specified number of seconds

**Python:**
```python
import time
time.sleep(5)
time.sleep(0.2)
```

**VimScript:**
```vim
" sleep for 5 seconds
sleep 5
" sleep for 200 milliseconds
sleep 200m
```

*Help:* [:sleep](https://vimhelp.org/various.txt.html#%3asleep)

------------------------------------------------------------------------------

## JSON encoder and decoder

**Python:**
```python
import json
v = ['foo', {'a' : 3}, True]
# encode data to JSON
str = json.dumps(v)
# decode data from JSON
x = json.loads(str)
```

**VimScript:**
```vim
let v = ['foo', {'a' : 3}, v:true]
" encode data to JSON
let str = v->json_encode()
echo str
" decode data from JSON
let x = str->json_decode()
echo x
```

*Help:* [json_encode()](https://vimhelp.org/builtin.txt.html#json_encode%28%29), [json_decode()](https://vimhelp.org/builtin.txt.html#json_decode%28%29), [js_encode()](https://vimhelp.org/builtin.txt.html#js_encode%28%29), [js_decode()](https://vimhelp.org/builtin.txt.html#js_decode%28%29)

------------------------------------------------------------------------------

## Network Sockets

**Python:**
```python
import requests
# Get a web page from http://httpbin.org
def Display_Page():
  r = requests.get('http://httpbin.org/')
  if r.status_code == requests.codes.ok:
    # display the received header and contents
    print(r.headers)
    print(r.text)
  else:
    print("Error: Failed to open URL")
Display_Page()
```

**VimScript:**
```vim
let g:rcvd_data = []

" channel data callback function. Called for every received line.
func Chan_data_cb(ch, msg)
  call add(g:rcvd_data, a:msg)
endfunc

" channel close callback function.
func Chan_close_cb(ch)
  echo g:rcvd_data
endfunc

func Display_Page()
  let addr = "httpbin.org:80"
  let ch_opt = {}
  " data received is processed one line at a time
  let ch_opt.mode = 'nl'
  let ch_opt.waittime = -1
  let ch_opt.drop = "never"
  let ch_opt.callback = function('Chan_data_cb')
  let ch_opt.close_cb = function('Chan_close_cb')
  " open the channel
  let ch = ch_open(addr, ch_opt)
  if ch_status(ch) != "open"
    echomsg "Failed to open channel, status = " .. ch_status(ch)
    return
  endif
  " send a http request
  call ch_sendraw(ch, "GET / HTTP/1.0\nHost: httpbin.org\n\n")
endfunc

call Display_Page()
```

*Help:* [job-channel-overview](https://vimhelp.org/channel.txt.html#job-channel-overview), [ch_open()](https://vimhelp.org/channel.txt.html#ch_open%28%29), [ch_status()](https://vimhelp.org/channel.txt.html#ch_status%28%29), [ch_sendraw()](https://vimhelp.org/channel.txt.html#ch_sendraw%28%29), [ch_close()](https://vimhelp.org/channel.txt.html#ch_close%28%29)

------------------------------------------------------------------------------

## Background Processes

### Starting a background process and communicating with it

The 'bc' calculator utility is used in the below example for illustrative purposes only.

**Python:**
```python
import subprocess
procObj = subprocess.Popen('bc',
    stdin=subprocess.PIPE,
    stdout=subprocess.PIPE,
    shell=True)
lines, err = procObj.communicate("12 * 6\n")
print("Result = " + lines)
print("Exitcode = " + str(procObj.returncode))
```

**VimScript:**
```vim
let job = job_start('bc')
if job_status(job) != "run"
  echo "Failed to start bc"
else
  let output = ch_evalraw(job, "6 * 12\n")
  echo "Result =" output
  call job_stop(job, "kill")
  let info = job_info(job)
  echo "Exitcode = " info.exitval
endif
```

*Help:* [job](https://vimhelp.org/channel.txt.html#job), [job_start()](https://vimhelp.org/channel.txt.html#job_start%28%29), [job_status()](https://vimhelp.org/channel.txt.html#job_status%28%29), [job_stop()](https://vimhelp.org/channel.txt.html#job_stop%28%29), [job_info()](https://vimhelp.org/channel.txt.html#job_info%28%29)

------------------------------------------------------------------------------

## Unit Tests

**Python:**
```python
import unittest

class TestDemoMethods(unittest.TestCase):
    def test_abc(self):
        self.assertEqual('FOO'.lower(), 'foo')
        self.assertNotEqual(1, 2)
        self.assertTrue('foo' == 'foo')
        self.assertFalse('FOO' == 'foo')
        self.assertIsNot('foo', 1)
        self.assertRegex('ab123xy', '\d+')
        self.assertNotRegex('abcd', '\d+')
        with self.assertRaises(TypeError):
            'a:b'.split(2)

unittest.main()
```

**VimScript:**
```vim
let v:errors = []
call assert_equal(tolower('FOO'), 'foo')
call assert_notequal(1, 2)
call assert_true('foo' == 'foo')
call assert_false('FOO' == 'foo')
call assert_fails('let l = split("a:b", [])', 'E730:')
call assert_match('\d\+', 'ab123xy')
call assert_notmatch('\d\+', 'abcd')
if len(v:errors) == 0
    echo "Test passed"
else
    echo "Test failed: ", v:errors
endif
```

*Help:* [testing](https://vimhelp.org/testing.txt.html#testing), [v:errors](https://vimhelp.org/eval.txt.html#errors-variable)
